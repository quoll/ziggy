ROOM = <'<'> ws <'ROOM'> ws label ws FEATURE* ws <'>'> 
<FEATURE> = LOC | DESC | EXIT | ACTION | FLAGS | GLOBAL | THINGS
LOC = lp <'LOC'> ws label rp
DESC = lp <'DESC'> ws string rp
EXIT = lp DIRECTION ws ( <'TO'> ws label [IF-STATEMENT] | PER ) ws rp
PER = <'PER'> ws FUNCTION-LABEL 
IF-STATEMENT = <'IF'> ws label <'ELSE'> ws string
DIRECTION = 'EAST' | 'WEST' | 'NORTH' | 'SOUTH' | 'UP' | 'DOWN' | 'NORTHEAST' | 'NORTHWEST' | 'SOUTHWEST' | 'SOUTHEAST'
ACTION = lp <'ACTION'> ws FUNCTION-LABEL rp
FLAGS = lp <'FLAGS'> ws label+ rp
GLOBAL = lp <'GLOBAL'> ws label+ rp
THINGS = lp <'THINGS'> ws THINGSET label+ rp
THINGSET = <'<'> ws label* <'>'> ws
FUNCTION-LABEL = label
<lp> = <'('> ws
<rp> = <')'> ws
<label> = #"[\-_A-Z]+" ws
<string> = <'"'> #'[^"]*' <'"'> ws
<ws> = <#'\s'*>
